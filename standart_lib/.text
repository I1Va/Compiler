section .text

;-------------------------------------------------
; calculate sqrt
;
; Destr:    rax, rbx, rdx, rdi, rsi, r8, r9, r10, r11, xmm1
;
; Ret:      xmm0
;-------------------------------------------------
sqrt:
    movdqu xmm0, [rsp + 8]
    sqrtsd xmm0, xmm0        ; xmm0 = sqrt(xmm0)
    ret


;-------------------------------------------------
; make new line
;
; Destr:    rax, rdi, rsi, rdx
;
; Ret:      rax
;-------------------------------------------------

make_new_line:
                mov rax, 0x01           ; sys_write
                mov rdi, 0x01           ; stdout
                lea rsi, [NewLineChar]  ; '\n'
                mov rdx, 1              ; len = 1
                syscall

                ret

;-------------------------------------------------
; read int
;
; Destr:    rax, rbx, rdx, rdi, rsi, r8, r9, r10, r11, xmm1
;
; Ret:      rax
;-------------------------------------------------
intio_in:
                call doubleio_in
                cvttsd2si rax, xmm0
                ret

;-------------------------------------------------
; print int
;
; Destr:    rax, rbx, rdx, rdi, rsi, r8, r9, r10, r11, xmm1
;
; Ret:      None
;-------------------------------------------------

intio_out:
                mov rbx, [rsp + 8]
                cvtsi2sd xmm1, rbx

                sub rsp, 16
                movdqu [rsp], xmm1

                call doubleio_out

                add rsp, 16

                ret

;-------------------------------------------------
; read double
;
; Destr:    rax, rbx, rdx, rdi, rsi, r8, r9, r10, r11, xmm1
;
; Ret:      xmm0
;-------------------------------------------------
doubleio_in:

                mov rax, 0x01               ; sys_write
                mov rdi, 0x01               ; stdout
                lea rsi, [EnterDoubleStr]   ; string
                mov rdx, EnterDoubleStrLen  ; string len
                syscall

                mov rdi, [ReadBufferLen]
                cmp [ReadBufferPos], rdi
                jb .bufIsNotEmpty

                mov rax, 0x00               ; sys_read
                mov rdi, 0x00               ; stdin
                lea rsi, [ReadBuffer]       ; buffer
                mov rdx, BufCapacity        ; buffer size
                syscall

                mov rdi, rax
                mov qword [ReadBufferLen], rdi
                mov qword [ReadBufferPos], 0

.bufIsNotEmpty:
                mov rsi, [ReadBufferPos]

                ; rdi  - buf len
                ; rsi  - buf pos
                ; rbx  - cur symbol
                ; r8   - is negative
                ; r9   - is after floating point
                ; r10  - exp10
                ; r11  = 10
                ; rax  - number

                xor r8, r8
                xor r9, r9
                xor r10, r10
                xor rbx, rbx
                xor rax, rax
                mov r11, 10

                cmp rsi, rdi ; pos >= len
                jae .error

                cmp byte ReadBuffer[rsi], '-'
                jne .isPositive
                mov r8, 1
                inc rsi
                inc qword [ReadBufferPos] ; is needed for empty input check

.isPositive:
                jmp .whileClause
.whileBody:
                mul r11
                lea rbx, [rbx - '0']
                add rax, rbx

                add r10, r9 ; inc exp10
                inc rsi

.whileClause:
                cmp rsi, rdi ; pos >= len
                jae .endBuf

                mov bl, ReadBuffer[rsi] ; bl - cur symbol

                cmp bl, '0'
                jb .notDigit

                cmp bl, '9'
                ja .notDigit

                jmp .whileBody

.notDigit:
                cmp bl, '.'
                je .floatingPoint

                cmp bl, 0x0a
                je .endLine

                jmp .error

.floatingPoint:
                test r9, r9
                jne .error ; several floating points in one number

                mov r9, 1
                inc rsi
                inc qword [ReadBufferPos] ; is needed for empty input check

                jmp .whileClause

.endLine:
                cmp [ReadBufferPos], rsi
                je .error ; empty input

                inc rsi
                cmp rsi, rdi
                je .endBuf

                mov qword [ReadBufferPos], rsi
                jmp .writeAns

.endBuf:
                cmp [ReadBufferPos], rsi
                je .error ; empty input

                mov qword [ReadBufferLen], 0
                mov qword [ReadBufferPos], 0

.writeAns:
                test r8, r8
                je .isPositive2

                neg rax

.isPositive2:
                cvtsi2sd xmm0, rax

                mov rax, 1

                jmp .forExpClause
.forExpBody:
                dec r10
                mul r11

.forExpClause:
                test r10, r10
                jne .forExpBody

                cvtsi2sd xmm1, rax

                divsd xmm0, xmm1

                ret

.error:
                mov rax, 0x01           ; sys_write
                mov rdi, 0x01           ; stdout
                lea rsi, [InputError]   ; string
                mov rdx, InputErrorLen  ; string len
                syscall

                mov rax, 0x3c           ; exit
                mov rdi, -1             ; error code
                syscall
;-------------------------------------------------



;-------------------------------------------------
; print_string(len, string)
;
; Stack Args: size, str_lit
;
; Destr:
;-------------------------------------------------

strio_out:

                mov rax, 0x01       ; sys_write
                mov rdi, 0x01       ; stdout
                mov rsi, [rsp + 8]  ; string lit
                mov rdx, [rsp + 16] ; string size
                syscall

                call make_new_line

                ret


;-------------------------------------------------
; print double
;
; Args:     push xmm on stack
;
; Destr:    rax, rbx, rcx, rdx, rdi, rsi, r11, r12, r13, xmm0, xmm1
;-------------------------------------------------


doubleio_out:

                lea r13, [WriteBuffer + BufCapacity - 1]         ; r13 - output str pointer

                mov byte [r13], 0x0a ; \n
                dec r13

                movdqu xmm2, [rsp + 8]

                movq rbx, xmm2                              ; rbx - raw xmm2
                test rbx, rbx
                jns .isPositive ; xmm2 >= 0

                movq xmm1, xmm2
                pxor xmm2, xmm2
                subsd xmm2, xmm1
.isPositive:

                cvttsd2si r11, xmm2 ; convert float to int  ; r11 - int part
                cvtsi2sd xmm1, r11
                subsd xmm2, xmm1
                mulsd xmm2, [double_1e6]
                cvttsd2si rax, xmm2                         ; rax - non-int part

                ; convert non-int part
                mov r12d, 10    ; base
                mov rcx, 6      ; width
.flWhileBody:
                xor rdx, rdx
                div r12d
                ; eax = div
                ; edx = mod

                mov dl, HexTable[rdx]
                mov byte [r13], dl
                dec r13
                dec rcx

                test rcx,rcx
                jne .flWhileBody

                mov byte [r13], '.'
                dec r13

                mov rax, r11                                ; rax - int part

                ; convert int part
.intWhileBody:
                xor rdx, rdx
                div r12d
                ; eax = div
                ; edx = mod

                mov dl, HexTable[rdx]
                mov byte [r13], dl
                dec r13

                test eax, eax
                jne .intWhileBody

                test rbx, rbx
                jns .isPositive2 ; xmm0 >= 0

                mov byte [r13], '-'
                dec r13
.isPositive2:

                mov r11, WriteBuffer + BufCapacity - 1
                sub r11, r13

                inc r13

                mov rax, 0x01   ; sys_write
                mov rdi, 0x01   ; stdout
                mov rsi, r13    ; buffer
                mov rdx, r11    ; buffer size
                syscall

                ret
;-------------------------------------------------
